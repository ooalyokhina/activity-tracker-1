buildscript {
    ext.kotlin_version = "1.1.1"
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url 'https://files.liferay.com/mirrors/xuggle.googlecode.com/svn/trunk/repo/share/java/' }
    }
}
plugins {
    id "org.jetbrains.intellij" version "0.3.10"
    id "org.jetbrains.kotlin.jvm" version "1.1.1"
}
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "idea"
apply plugin: "org.jetbrains.intellij"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'lib' }
    maven { url 'https://files.liferay.com/mirrors/xuggle.googlecode.com/svn/trunk/repo/share/java/' }
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.8'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    compile "com.opencsv:opencsv:4.0"
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'org.slf4j:slf4j-log4j12:1.7.5'
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.1"
    compile "com.madgag:util-diff:1.28"
    compile "joda-time:joda-time:2.9.2"
    compile "org.apache.commons:commons-csv:1.3"
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile 'com.googlecode.lanterna:lanterna:3.0.1'
    compile 'xuggle:xuggle-xuggler:5.4'
    testCompile "junit:junit:4.11" // also includes hamcrest-core
}

sourceSets {
    main {
        kotlin { srcDir "./src/main" }
        resources { srcDir "./src/resources" }
    }
    test {
        kotlin { srcDir "./src/test" }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.1"
        languageVersion = "1.1"
        // Compiler flag to allow building against pre-released versions of Kotlin
        // because IJ EAP can be built using pre-released Kotlin but it's still worth doing to check API compatibility
        freeCompilerArgs = ["-Xskip-metadata-version-check"]
    }
}

intellij {
    def ideVersion = System.getenv().getOrDefault("ACTIVITY_TRACKER_IDEA_VERSION", "IC-172.3757.29")
    println("Using ide version: ${ideVersion}")
    version ideVersion
    // The artifact name is deliberately not "activity-tracker" because
    // *.csv files are stored in the "activity-tracker" directory.
    // If plugin archive has the same name, the directory will be deleted on plugin update.
    pluginName "activity-tracker-plugin"
    downloadSources true
    sameSinceUntilBuild false
    updateSinceUntilBuild false
}
